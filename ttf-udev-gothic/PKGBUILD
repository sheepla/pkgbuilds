# Maintainer: sheepla <hk7g9x43 at anonaddy dot me>

pkgname=ttf-udev-gothic
pkgver=1.1.0
pkgrel=1
pkgdesc="A Japanese programming font based on JetBrains Mono and BIZ UD Gothic"
url="https://github.com/yuru7/udev-gothic"
license=("custom:OFL" "MIT")
arch=("any")
source=(
  "${pkgname}-${pkgver}.zip::${url}/releases/download/v${pkgver}/UDEVGothic_v${pkgver}.zip"
  "${pkgname}-nf-${pkgver}.zip::${url}/releases/download/v${pkgver}/UDEVGothic_NF_v${pkgver}.zip"
  "LICENSE::https://raw.githubusercontent.com/yuru7/udev-gothic/v${pkgver}/LICENSE"
  "README.md::https://raw.githubusercontent.com/yuru7/udev-gothic/v${pkgver}/README.md"
)
sha256sums=('959f4a687ce960ba8f67ce323843c119373a923c1b6216c944bf34c8a641536e'
            '13763a5d7d0cf2a025740b910820933af745a1734baf730c910fad82ffa85178'
            '3e04c747c9c9535224f6163a290caf022877759db5183a00aef6134d3f5643f0'
            '6ee82c02eeb49911480b54f7d5f0dd50708796f64ac015e01889dbd8f138e2bc')

package() {
  cd "${srcdir}"

  _ttf_dir="${pkgdir}/usr/share/fonts/TTF"
  find "${srcdir}" -type f -name '*.ttf' -print0 | xargs -0 install -Dm644 -t "${_ttf_dir}"

  _licenses_dir="${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm644 LICENSE "${_licenses_dir}/LICENSE"

  _doc_dir="${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm644 README.md "${_doc_dir}/README.md"
}
